plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

ext {
    mapstructVersion = "1.5.3.Final"
    lombokVersion = "1.18.24"
    lombokMapstructBindingVersion = "0.2.0"
    slf4jVersion = "2.0.6"
    kafkaVersion = "3.3.1"
    twitterApiV2Version = "2.0.3"
    googleGuavaVersion = "31.1-jre"
    log4jVersion = "2.19.0"

    junitVersion = "5.8.2"
    mockitoVersion = "4.11.0"
    hamcrestVersion = "1.3"
    awaitilityVersion = "4.2.0"
}

compileJava {
    sourceCompatibility = 11
    targetCompatibility = 11
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.slf4j:slf4j-log4j12:${slf4jVersion}"
    implementation "org.apache.kafka:connect-api:${kafkaVersion}"
    implementation "com.google.guava:guava:${googleGuavaVersion}"
    implementation "com.twitter:twitter-api-java-sdk:${twitterApiV2Version}"

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.mockito:mockito-inline:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.hamcrest:hamcrest-all:${hamcrestVersion}"
    testImplementation "org.awaitility:awaitility:${awaitilityVersion}"
    testImplementation "org.slf4j:slf4j-simple:${slf4jVersion}"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

test {
    useJUnitPlatform()
}

tasks.register('copyConfig', Copy) {
    from "${projectDir}/config"
    filter { it.replaceAll('@twitterBearerToken@', twitterBearerToken) }
    into "${buildDir}/config"
}

tasks.register('copyRuntimeLibs', Copy) {
    from configurations.runtimeClasspath
    {
        // This is a transitive dependency from the Twitter API library, but it gets a different
        // version from that needed by Kafka Connect
        exclude group: "javax.ws.rs"
    }
    into "${buildDir}/libs"
}

tasks.register('dist', Zip) {
    dependsOn build
    from('build/libs') {
        into 'libs'
    }
    from('config') {
        into 'config'
    }
    from('bin') {
        into 'bin'
    }
    from('sdk.properties')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact source: dist, extension: 'zip'
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/adam-crowther/kafka-connect-source-twitter-api-v2"
            credentials {
                username = project.property "github.actor"
                password = project.property "github.token"
            }
        }
    }
}

build {
    dependsOn copyConfig
    dependsOn copyRuntimeLibs
}